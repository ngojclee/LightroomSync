#ifndef AppVersion
  #define AppVersion "2.0.0.0"
#endif

#ifndef SourceBinDir
  #define SourceBinDir "..\\build\\bin"
#endif

#ifndef OutputDir
  #define OutputDir "..\\build\\installer"
#endif

#ifndef UIRuntime
  #define UIRuntime "harness"
#endif

#ifndef UIRuntimeRequested
  #define UIRuntimeRequested UIRuntime
#endif

#ifndef UIBinaryName
  #define UIBinaryName "LightroomSync.exe"
#endif

[Setup]
AppId={{8EDE9ED8-3514-492D-AF64-4E5FC856D636}
AppName=Lightroom Sync
AppVersion={#AppVersion}
AppVerName=Lightroom Sync {#AppVersion}
DefaultDirName={autopf64}\Lightroom Sync
DisableDirPage=no
DefaultGroupName=Lightroom Sync
DisableProgramGroupPage=yes
UninstallDisplayIcon={app}\{#UIBinaryName}
OutputDir={#OutputDir}
OutputBaseFilename=LightroomSyncSetup-v{#AppVersion}-windows-amd64
Compression=lzma2
SolidCompression=yes
ArchitecturesAllowed=x64compatible
ArchitecturesInstallIn64BitMode=x64compatible
PrivilegesRequired=admin
WizardStyle=modern
UsePreviousAppDir=yes
UsePreviousTasks=yes
CloseApplications=yes
RestartApplications=no
AppMutex=LightroomSyncAgent_Mutex

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "Create a desktop shortcut"; GroupDescription: "Additional shortcuts"

[Files]
Source: "{#SourceBinDir}\LightroomSyncAgent.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceBinDir}\{#UIBinaryName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceBinDir}\build-metadata.json"; DestDir: "{app}"; Flags: ignoreversion skipifsourcedoesntexist

[Icons]
Name: "{autoprograms}\Lightroom Sync"; Filename: "{app}\{#UIBinaryName}"; WorkingDir: "{app}"
Name: "{autodesktop}\Lightroom Sync"; Filename: "{app}\{#UIBinaryName}"; WorkingDir: "{app}"; Tasks: desktopicon

[Registry]
Root: HKA; Subkey: "Software\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "LightroomSync"; ValueData: """{app}\LightroomSyncAgent.exe"" --minimized"; Flags: uninsdeletevalue

[Run]
Filename: "{app}\{#UIBinaryName}"; Description: "Launch Lightroom Sync"; Flags: nowait postinstall skipifsilent runasoriginaluser

[Code]
function TryStopProcess(const ImageName: string; const ForceKill: Boolean): Boolean;
var
  ResultCode: Integer;
  Params: string;
begin
  if ForceKill then
    Params := '/IM "' + ImageName + '" /T /F'
  else
    Params := '/IM "' + ImageName + '" /T';

  Result := Exec(ExpandConstant('{sys}\taskkill.exe'), Params, '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
  if Result then
    Log(Format('taskkill executed for %s (force=%d), exit=%d', [ImageName, Ord(ForceKill), ResultCode]))
  else
    Log(Format('taskkill failed to start for %s (force=%d)', [ImageName, Ord(ForceKill)]));
end;

procedure StopRunningProcesses();
begin
  Log('Stopping Lightroom Sync processes before install/uninstall...');
  TryStopProcess('LightroomSync.exe', False);
  TryStopProcess('LightroomSyncUI.exe', False);
  TryStopProcess('LightroomSyncAgent.exe', False);
  Sleep(1200);
  TryStopProcess('LightroomSync.exe', True);
  TryStopProcess('LightroomSyncUI.exe', True);
  TryStopProcess('LightroomSyncAgent.exe', True);
  Sleep(400);
end;

function PrepareToInstall(var NeedsRestart: Boolean): String;
begin
  StopRunningProcesses();
  Result := '';
end;

function InitializeSetup(): Boolean;
begin
  Log(Format('Installer UI runtime requested=%s effective=%s', ['{#UIRuntimeRequested}', '{#UIRuntime}']));
  Result := True;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usUninstall then
    StopRunningProcesses();
end;
